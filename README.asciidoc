*ATTENTION: as of version `3.2.5` of this plugin we are using the link:https://github.com/project-aries/docker-java-shaded[docker-java-shaded] library, in favor of `docker-java`, which itself is hosted on jcenter ONLY. This means that instead of resolving the multiple, potentially conflicting, dependencies of `docker-java` we will instead resolve 1 shaded/uber/fat jar with all dependencies baked in. The dependencies/classes themselves are now located under the `com.github.dockerjava.shaded` package. Please give things a go and report back any issues found. Thanks!*

image:https://travis-ci.org/bmuschko/gradle-docker-plugin.svg?branch=master["Build Status", link="https://travis-ci.org/bmuschko/gradle-docker-plugin"]
image:https://api.bintray.com/packages/bmuschko/gradle-plugins/com.bmuschko%3Agradle-docker-plugin/images/download.svg["Download", link="https://bintray.com/bmuschko/gradle-plugins/com.bmuschko%3Agradle-docker-plugin/_latestVersion"]
image:https://img.shields.io/badge/stack%20overflow-gradle%20docker%20plugin-blue.svg["Stackoverflow", link="https://stackoverflow.com/questions/tagged/gradle-docker-plugin"]
image:https://badges.gitter.im/Join%20Chat.svg["Gitter", link="https://gitter.im/gradle-docker-plugin/Lobby?utm_source=badge&utm_medium=badge&utm_campaign=pr-badge"]

= Gradle Docker plugin

image:https://www.docker.com/sites/default/files/mono-horizontal.png[Docker Logo]

Gradle plugin for managing link:https://www.docker.io/[Docker] images and containers using via its
link:http://docs.docker.io/reference/api/docker_remote_api/[remote API]. The heavy lifting of communicating with the
Docker remote API is handled by the link:https://github.com/docker-java/docker-java[Docker Java library]. Please
refer to the library's documentation for more information on the supported Docker's client API and Docker server version.

*This plugin requires Gradle >= 2.5 to work properly.*

== Usage

To use the plugin, include in your build script:

[source,groovy]
----
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:X.Y.Z'
    }
}
----


== Provided plugins

The JAR file comes with two plugins:

[options="header"]
|=======
|Plugin Id                            |Automatically applies          |Type                                                                                                                                                        |Description
|com.bmuschko.docker-remote-api       |-                              |link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/DockerRemoteApiPlugin.html[DockerRemoteApiPlugin]             |Provides custom tasks for interacting with Docker via its remote API.
|com.bmuschko.docker-java-application |com.bmuschko.docker-remote-api |link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/DockerJavaApplicationPlugin.html[DockerJavaApplicationPlugin] |Creates and pushes a Docker image for a Java application.
|com.bmuschko.docker-spring-boot-application |com.bmuschko.docker-remote-api |link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/DockerSpringBootApplicationPlugin.html[DockerSpringBootApplicationPlugin] |Creates and pushes a Docker image for a Spring Boot application.
|=======

== Remote API plugin

The plugin `com.bmuschko.docker-remote-api` allows for interacting with Docker via its remote API. You can model any workflow
imaginable by creating enhanced task of the custom task provided by the plugin. To use the plugin, include the following
code snippet in your build script:

[source,groovy]
----
apply plugin: 'com.bmuschko.docker-remote-api'
----

If you would like to use this plugin from a build file which is not the *main* build file you must apply/use it like this:

.build.gradle
[source,groovy]
----
apply from: 'gradle/docker.gradle'
----

.gradle/docker.gradle
[source,groovy]
----
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:X.Y.Z'
    }
}

// use fully qualified class name
apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

// import task classes
import com.bmuschko.gradle.docker.tasks.image.*

// use task classes
----

The plugin automatically resolves the Docker Java library with the pre-configured version under the covers. The only
configuration you will have to provide in your build script is the repository hosting the library and its transitive
dependencies. One repository that hosts them all is Maven Central.

[source,groovy]
----
repositories {
    mavenCentral()
}
----

=== Custom task types

==== Misc

The plugin provides the following general-purpose custom task types:

[options="header"]
|=======
|Type                                                                                                                                  |Description
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/DockerClient.html[DockerClient]   |Passes the raw docker-java client to the onNext closure if it's defined.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/DockerInfo.html[DockerInfo]       |Displays system-wide information.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/DockerVersion.html[DockerVersion] |Show the docker version information.
|=======


==== Images

The plugin provides the following custom task types for managing images:

[options="header"]
|=======
|Type                                                                                                                                                  |Description
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/Dockerfile.html[Dockerfile]                 |Creates a Dockerfile based on the provided instructions.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/DockerBuildImage.html[DockerBuildImage]     |Builds an image from a Dockerfile.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/DockerCommitImage.html[DockerCommitImage]   |Creates a new image from a container's changes.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/DockerInspectImage.html[DockerInspectImage] |Returns low-level information on the image.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/DockerListImages.html[DockerListImages]     |Lists images in registry.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/DockerPullImage.html[DockerPullImage]       |Pulls an image from the registry.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/DockerPushImage.html[DockerPushImage]       |Pushes an image to a registry.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/DockerRemoveImage.html[DockerRemoveImage]   |Removes an image from the filesystem.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/DockerTagImage.html[DockerTagImage]         |Tags an image in registry.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/DockerSaveImage.html[DockerSaveImage]       |Saves an image to file.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/DockerLoadImage.html[DockerLoadImage]       |Loads an image from file.
|=======


==== Containers

The plugin provides the following custom task types for managing containers:

[options="header"]
|=======
|Type                                                                                                                                                                        |Description
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerCopyFileToContainer.html[DockerCopyFileToContainer]     |Copies a path from the host into the container.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerCopyFileFromContainer.html[DockerCopyFileFromContainer] |Copies a path from the container as a tar file on to the host.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerCreateContainer.html[DockerCreateContainer]             |Creates a container.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerInspectContainer.html[DockerInspectContainer]           |Returns low-level information on the container.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerKillContainer.html[DockerKillContainer]                 |Kills the container for a given id.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerRemoveContainer.html[DockerRemoveContainer]             |Removes the container for a given id from the filesystem.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerRenameContainer.html[DockerRenameContainer]             |Rename a container.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerRestartContainer.html[DockerRestartContainer]           |Restarts the container for a given id.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerStartContainer.html[DockerStartContainer]               |Starts the container for a given id.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerStopContainer.html[DockerStopContainer]                 |Stops the container for a given id.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerWaitContainer.html[DockerWaitContainer]                 |Blocks until container for a given id stops.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerLogsContainer.html[DockerLogsContainer]                 |Copies the container output to the Gradle process standard out/err.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerExecContainer.html[DockerExecContainer]                 |Executes a command within a running container.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/DockerInspectExecContainer.html[DockerInspectExecContainer]   |Inspects task executed inside container with DockerExecContainer command.
|=======


==== Networks

The plugin provides the following custom task types for managing networks:

[options="header"]
|=======
|Type                                                                                                                                                        |Description
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/network/DockerCreateNetwork.html[DockerCreateNetwork]   |Creates a network.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/network/DockerInspectNetwork.html[DockerInspectNetwork] |Returns low-level information on the network.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/network/DockerRemoveNetwork.html[DockerRemoveNetwork]   |Removes the network.
|=======


==== Extras

The plugin provides the following additional tasks:

[options="header"]
|=======
|Type                                                                                                                                                                             |Description
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/extras/DockerExecStopContainer.html[DockerExecStopContainer] |Shut down container with cmd, polling for it to enter a non-running state, and if that does not succeed in time issue stop request.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/extras/DockerLivenessContainer.html[DockerLivenessContainer] |Polls an arbitrary containers logs for a message indicating liveness.
|link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/container/extras/DockerWaitHealthyContainer.html[DockerWaitHealthyContainer] |Blocks until the container for a given id becomes link:https://docs.docker.com/engine/reference/builder/#healthcheck[healthy].
|=======


=== Extension properties

The plugin defines the following extension properties in the `docker` closure:

[options="header"]
|=======
|Property name   |Type      |Default value            |Description
|`url`           |String    |see below                |The server URL to connect to via Docker's remote API.
|`certPath`      |File      |null                     |The path to certificates for communicating with link:https://docs.docker.com/articles/https/[Docker over SSL].
|`apiVersion`    |String    |null                     |The remote API version. For most cases this can be left null.
|=======

*The default value is now generated based on a best guess attempt given the link:https://github.com/bmuschko/gradle-docker-plugin/blob/master/src/main/groovy/com/bmuschko/gradle/docker/DockerExtension.groovy#L42[OperatingSystem and Environment]:*

    Unix-based machine: unix:///var/run/docker.sock
    Windows-based machine (and everything else): tcp://127.0.0.1:2375

Image pull or push operations against the public Docker Hub registry or a private registry may require authentication.
You can provide those credentials in the `registryCredentials` closure:

[options="header"]
|=======
|Property name   |Type      |Default value               |Description
|`url`           |String    |https://index.docker.io/v1/ |The registry URL.
|`username`      |String    |null                        |The registry username.
|`password`      |String    |null                        |The registry password.
|`email`         |String    |null                        |The registry email address.
|=======


==== Extension examples

===== Working with a TLS-enabled Docker instance

Starting with Docker version 1.3, TLS is enabled by default. Please consult the Docker documentation link:https://docs.docker.com/articles/https/["Running Docker
with https"] to set up your certificate. The following example demonstrates how to configure the plugin to use those certificates.
Additionally, this code snippet shows how to set the user credentials.

[source,groovy]
----
docker {
    url = 'https://192.168.59.103:2376'
    certPath = new File(System.properties['user.home'], '.boot2docker/certs/boot2docker-vm')

    registryCredentials {
        url = 'https://index.docker.io/v1/'
        username = 'bmuschko'
        password = 'pwd'
        email = 'benjamin.muschko@gmail.com'
    }
}
----

===== Working with Google Cloud and using a key file

[source,groovy]
----
docker {
    registryCredentials {
        url = 'https://gcr.io'
        username = '_json_key'
        password = file('keyfile.json").text
    }
}
----

===== Working with a Docker instance without TLS

The following example assumes that you disabled TLS on your Docker instance. You can do so by setting `DOCKER_TLS=no` in the file
 `/var/lib/boot2docker/profile`.

[source,groovy]
----
docker {
    url = 'tcp://192.168.59.103:2375'
}
----
On Unix the Docker daemon listens by default on `unix:///var/run/docker.sock`.

On Windows the Docker daemon listens by default on `npipe:////./pipe/docker_engine` though this is not currently supported. We instead fall back to `tcp://127.0.0.1:2375`.

=== Usage examples

The following usage examples demonstrate code for common use cases. More scenarios can be found in the link:https://github.com/bmuschko/gradle-docker-plugin/blob/master/src/functTest/groovy/com/bmuschko/gradle/docker/DockerWorkflowFunctionalTest.groovy[functional tests].

==== Creating a Dockerfile and building an image

A Dockerfile can be created by the `Dockerfile` custom tasks. The Dockerfile instructions need to be declare in the correct
order.

[source,groovy]
----
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

task createDockerfile(type: Dockerfile) {
    destFile = project.file('build/mydockerfile/Dockerfile')
    from 'ubuntu:12.04'
    maintainer 'Benjamin Muschko "benjamin.muschko@gmail.com"'
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = 'bmuschko/myimage:latest'
}
----

== Reactive-Streams

As needed, we will implement reactive methods as described in link:https://github.com/reactive-streams/reactive-streams-jvm[reactive-streams].
We implement these here as optional closures for all tasks. Currently the only supported methods are `onError`, `onNext`, `onComplete`. Various examples
on how to use these can be found in our link:https://github.com/bmuschko/gradle-docker-plugin/blob/master/src/functTest/groovy/com/bmuschko/gradle/docker/DockerReactiveMethodsFunctionalTest.groovy[reactive tests]

==== onError

The `onError` closure is passed the exception that is thrown for YOU to handle. If you silently ignore we will not throw the exception behind the scenes.
The below example is a common use-case that arises when someone wants to remove a container whether it exists or not but does not want to fail hard.

[source,groovy]
----
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*

task removeContainer(type: DockerRemoveContainer) {
    targetContainerId { "container-that-does-not-exist" }
    onError { exception ->
        if (!exception.message.contains('No such container')) // ignore exception if container does not exist otherwise throw it
            throw exception
    }
}
----

==== onNext

The `onNext` closure is passed the next iterative response upon execution. For all other tasks we simply hand you back the object that is given to us by `docker-java`
which is a pojo representation of the json handed back by `docker`. Thus, and much like the `onException` closure, all delegation is now in your control. Any properties/values
expected to be set will not be done unless YOU do them.

Iterative tasks are things like `DockerBuildImage`, `DockerLogsContainer`, `DockerListImages`. These tasks have output which can be iterated over. The example below demonstrates how
we iterate over each log message passing that to the closure for the user to work on.

[source,groovy]
----
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*

task logContainer(type: DockerLogsContainer) {
    targetContainerId { "container-that-does-exist" }
    follow = true
    tailAll = true
    onNext { message ->
        logger.quiet message.toString() // each log message from the container will be passed as it's made available
    }
}
----

==== onComplete

The `onComplete` closure is not passed anything upon execution. It works in the same fashion that `doLast` does but is instead part of this task and thus executes
before `doLast` does. This closure executes ONLY upon success. The below example demonstrates how this works.

[source,groovy]
----
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*

task removeContainer(type: DockerRemoveContainer) {
    targetContainerId { "container-that-does-exist" }
    onComplete {
        println "Executes first"
    }
    doLast {
        println "Executes second"
    }
}
----

== Executing functional tests against a running container

The following example code demonstrates how to build a Docker image from a Dockerfile, starts up a container for this
image and exercises functional tests against the running container. At the end of this operation, the container is stopped.

[source,groovy]
----
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

task buildMyAppImage(type: DockerBuildImage) {
    inputDir = file('docker/myapp')
    tag = 'test/myapp:latest'
}

task createMyAppContainer(type: DockerCreateContainer) {
    dependsOn buildMyAppImage
    targetImageId { buildMyAppImage.getImageId() }
    portBindings = ['8080:8080']
}

task startMyAppContainer(type: DockerStartContainer) {
    dependsOn createMyAppContainer
    targetContainerId { createMyAppContainer.getContainerId() }
}

task stopMyAppContainer(type: DockerStopContainer) {
    targetContainerId { createMyAppContainer.getContainerId() }
}

task functionalTestMyApp(type: Test) {
    dependsOn startMyAppContainer
    finalizedBy stopMyAppContainer
}
----

=== Linking with other containers

In many situations your container does not start without dependencies like database. In that case you may wish using traditional linking:

[source,groovy]
----
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

task buildMyAppImage(type: DockerBuildImage) {
    inputDir = file('docker/myapp')
    tag = 'test/myapp'
}

task createDBContainer(type: DockerCreateContainer) {
    targetImageId { 'postgres:latest' }
    containerName "docker_auto_${buildMyAppImage.buildTagName}"
}

task createMyAppContainer(type: DockerCreateContainer, dependsOn: [buildMyAppImage, createDBContainer]) {
    targetImageId { buildMyAppImage.getImageId() }
    portBindings = ['8080:8080']

    doFirst{ // doFirst required! #319
        links = [ "${createDBContainer.getContainerId()}:database" ] // `database` there will be host used by application to DB connect
    }

    // If you use Systemd in containers you should also add lines: (#320):
    binds = [ '/sys/fs/cgroup': '/sys/fs/cgroup' ]
    tty = true
}

task startMyAppContainer(type: DockerStartContainer, dependsOn: createMyAppContainer) {
    targetContainerId { createMyAppContainer.getContainerId() }
}

task stopMyAppContainer(type: DockerStopContainer) {
    targetContainerId { createMyAppContainer.getContainerId() }
}

task functionalTestMyApp(type: Test, dependsOn: startMyAppContainer) {
    finalizedBy stopMyAppContainer
}
----

== Java application plugin

The plugin `com.bmuschko.docker-java-application` is a highly opinonated plugin for projects applying the link:http://www.gradle.org/docs/current/userguide/application_plugin.html[application plugin].
Under the covers the plugin preconfigures tasks for creating and pushing Docker images for your Java application. The default
configuration is tweakable via an exposed extension. To use the plugin, include the following code snippet in your build script:

[source,groovy]
----
apply plugin: 'com.bmuschko.docker-java-application'
----


=== Extension properties

The plugin defines the following extension properties in the `javaApplication` closure:

[options="header"]
|=======
|Property name   |Type      |Default value                                          |Description
|`baseImage`     |String    |openjdk                                                |The Docker base image used for Java application.
|`exec`          |Closure   |Create ENTRYPOINT using script from Application plugin |The ENTRYPOINT and/or CMD Dockerfile instructions
|`maintainer`    |String    |Value of `user.home` system property                   |The name and email address of the image maintainer (Deprecated).
|`port`          |Integer   |8080                                                   |The Docker image entry point port used for the Java application (Deprecated)
|`ports`         |Integer[] |[]                                                     |The Docker image exposed ports (if provided, this values will override `port` configuration property)
|`tag`           |String    |<project.group>/<applicationName>:<project.version>    |The tag used for the Docker image.
|=======


=== Usage example

[source,groovy]
----
docker {
    javaApplication {
        baseImage = 'dockerfile/java:openjdk-7-jre'
        maintainer = 'Benjamin Muschko "benjamin.muschko@gmail.com"'
        ports = [9090, 5701]
        tag = 'jettyapp:1.115'
    }
}
----

=== `exec` property
The `exec` extension property accepts a Closure which delegates to the link:http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/Dockerfile.CompositeExecInstruction.html[Dockerfile.CompositeExecInstruction] subset of the http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/Dockerfile.html[Dockerfile task].
It allows for specifying custom ENTRYPOINT or CMD commands at an appropriate location in the Dockerfile.

==== Usage example
[source,groovy]
----
docker {
    javaApplication {
        baseImage = 'dockerfile/java:openjdk-7-jre'
        maintainer = 'Benjamin Muschko "benjamin.muschko@gmail.com"'
        ports = [9090, 5701]
        tag = 'jettyapp:1.115'
        exec {
            defaultCommand 'server'
            entryPoint 'myApp/bin/containerLaunch.sh'
        }
    }
}
----

=== Default tasks

The plugin provides a set of tasks for your project and preconfigures them with sensible defaults.

[options="header"]
|=======
|Task name                 |Depends On                |Type                                                                                 |Description
|`dockerCopyDistResources` |`distTar`                 |link:http://www.gradle.org/docs/current/javadoc/org/gradle/api/tasks/Sync.html[Sync] |Copies the resource files (like the Java application's TAR file) to a temporary directory for image creation.
|`dockerDistTar`           |`dockerCopyDistResources` |Dockerfile                                                                           |Creates the Docker image for the Java application.
|`dockerBuildImage`        |`dockerDistTar`           |DockerBuildImage                                                                     |Builds the Docker image for the Java application.
|`dockerPushImage`         |`dockerBuildImage`        |DockerPushImage                                                                      |Pushes created Docker image to the repository.
|=======


=== Usage examples

The following usage examples demonstrate code for common use cases. More scenarios can be found in the link:https://github.com/bmuschko/gradle-docker-plugin/blob/master/src/functTest/groovy/com/bmuschko/gradle/docker/DockerJavaApplicationPluginFunctionalTest.groovy[functional tests].

[source,groovy]
----
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

version = '1.0'
sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.eclipse.jetty.aggregate:jetty-all:9.2.5.v20141112'
}

mainClassName = 'com.bmuschko.gradle.docker.application.JettyMain'

docker {
    javaApplication {
        maintainer = 'Jon Doe "jon.doe@gmail.com"'
    }
}
----

=== Additional instructions in Dockerfile

You can add additional instructions to the dockerfile using `dockerDistTar` and http://bmuschko.github.io/gradle-docker-plugin/docs/groovydoc/com/bmuschko/gradle/docker/tasks/image/Dockerfile.html[Dockerfile task] dsl:

[source,groovy]
----
dockerDistTar {
    instruction {'RUN ls -la'}
    environmentVariable('JAVA_OPTS', '-XX:+UnlockExperimentalVMOptions -XX:+UseCGroupMemoryLimitForHeap')
}

docker {
    javaApplication {
    }
}
----

will result in

----
FROM java
MAINTAINER user
ADD javaapp-1.0.0-SNAPSHOT.tar /
ENTRYPOINT ["/javaapp-1.0.0-SNAPSHOT/bin/javaapp"]
EXPOSE 8080
RUN ls -la
----

Or you can use form

[source,groovy]
----
project.tasks.getByName("dockerDistTar").instructionsFromTemplate file("Dockerfile.tmpl")
----

== Spring Boot application plugin

The plugin `com.bmuschko.docker-spring-boot-application` is a highly opinionated plugin for projects applying the link:https://docs.spring.io/spring-boot/docs/current/reference/html/build-tool-plugins-gradle-plugin.html[Spring Boot plugin].
Under the covers the plugin preconfigures tasks for creating and pushing Docker images for your Spring Boot application. The default
configuration is tweakable via an exposed extension. The plugin link:https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/html/#reacting-to-other-plugins[reacts] to either the `java` or `war` plugin. To use the plugin, include the following code snippet in your build script:

[source,groovy]
----
apply plugin: 'war'
apply plugin: 'com.bmuschko.docker-spring-boot-application'
----


=== Extension properties

The plugin defines the following extension properties in the `springBootApplication` closure:

[options="header"]
|=======
|Property name   |Type      |Default value                                          |Description
|`baseImage`     |String    |openjdk                                                |The Docker base image used for the Spring Boot application.
|`ports`         |Integer[] |[8080]                                                 |The Docker image exposed ports.
|`tag`           |String    |<project.group>/<applicationName>:<project.version>    |The tag used for the Docker image.
|=======


=== Usage example

[source,groovy]
----
docker {
    springBootApplication {
        baseImage = 'openjdk:8-alpine'
        ports = [9090, 8080]
        tag = 'awesome-spring-boot:1.115'
    }
}
----


=== Default tasks

The plugin provides a set of tasks for your project and preconfigures them with sensible defaults.

[options="header"]
|=======
|Task name                 |Depends On                |Type                                                                                 |Description
|`dockerSyncArchive`       |`assemble`                |link:http://www.gradle.org/docs/current/javadoc/org/gradle/api/tasks/Sync.html[Sync] |Copies the Spring Boot archive to a temporary directory for image creation.
|`dockerCreateDockerfile`  |`bootJar` or `bootWar`    |Dockerfile                                                                           |Creates the Docker image for the Spring Boot application.
|`dockerBuildImage`        |`dockerCreateDockerfile`  |DockerBuildImage                                                                     |Builds the Docker image for the Spring Boot application.
|`dockerPushImage`         |`dockerBuildImage`        |DockerPushImage                                                                      |Pushes created Docker image to the repository.
|=======


=== Usage examples

The following usage examples demonstrate code for common use cases. More scenarios can be found in the link:https://github.com/bmuschko/gradle-docker-plugin/blob/master/src/functTest/groovy/com/bmuschko/gradle/docker/DockerSpringBootApplicationPluginFunctionalTest.groovy[functional tests].

[source,groovy]
----
plugins {
    id 'war'
    id 'org.springframework.boot' version '2.0.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
    id 'com.bmuschko.docker-spring-boot-application'
}

version = '1.0'
sourceCompatibility = 8
targetCompatibility = 8

repositories {
    jcenter()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    providedRuntime 'org.apache.tomcat.embed:tomcat-embed-jasper'
}

docker {
    springBootApplication {
        baseImage = 'openjdk:8-alpine'
    }
}
----

== FAQ

=== Executing the plugin's test suite with custom configuration

The default setup can be configured with the help of the properties shown in the table below:

[options="header"]
|=======
|Description                 |System/Project/Environment variable          |Default Value
|Docker server URL           |dockerServerUrl/dockerServerUrl/DOCKER_HOST          |unix:///var/run/docker.sock
|Docker cert path            |dockerCertPath/dockerCertPath/DOCKER_CERT_PATH           |null
|Docker private registry URL |dockerPrivateRegistryUrl/dockerPrivateRegistryUrl/DOCKER_REGISTRY_HOST |http://localhost:5000
|=======

The following usage examples demonstrates running functional tests against the a docker instance:

[source,shell]
----
./gradlew build functionalTest

OR

./gradlew build functionalTest -PdockerServerUrl=unix:///var/run/docker.sock

OR

./gradlew build functionalTest -DdockerServerUrl=unix:///var/run/docker.sock

OR

export DOCKER_HOST=unix:///var/run/docker.sock && ./gradlew build functionalTest

OR

./gradlew build functionalTest -PdockerServerUrl=http://192.168.59.103:2376
----

==== Using a Vagrant box to bootstrap Docker

Docker does not need to be installed on the local or another remote machine. This project provides a
link:https://www.vagrantup.com/[Vagrant] image with the proper setup to bootstrap a Docker installation. The
link:https://github.com/bmuschko/gradle-docker-plugin/blob/master/vagrant/Vagrantfile[Vagrantfile] can be found under
the directory `vagrant`. To use the Vagrant box simply start it manually.

[source,shell]
----
vagrant up
----

==== Automatically starts and stop Vagrant box for executing functional tests

An installation of VirtualBox and Vagrant is required. See the link:https://www.vagrantup.com/docs/getting-started/["Getting Started"]
guide for more information.

Alternatively, you can configure the project to bootstrap the Vagrant box
as needed. Use the command line option `-PbootstrapDocker=true` for this purpose.

[source,shell]
----
./gradlew functionalTest -PbootstrapDocker=true
----
