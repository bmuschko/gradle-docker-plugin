buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'maven-publish'
apply plugin: com.jfrog.bintray.gradle.BintrayPlugin

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar
            artifact javadocJar

            pom {
                name = 'Gradle Docker plugin'
                description = 'Gradle plugin for managing Docker images and containers.'
                url = 'https://github.com/bmuschko/gradle-docker-plugin'
                inceptionYear = '2014'

                scm {
                    url = 'https://github.com/bmuschko/gradle-docker-plugin'
                    connection = 'scm:https://bmuschko@github.com/bmuschko/gradle-docker-plugin.git'
                    developerConnection = 'scm:git://github.com/bmuschko/gradle-docker-plugin.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'bmuschko'
                        name = 'Benjamin Muschko'
                        url = 'https://github.com/bmuschko'
                    }
                    developer {
                        id = 'cdancy'
                        name = 'Christopher Dancy'
                        url = 'https://github.com/cdancy'
                    }
                    developer {
                        id = 'orzeh'
                        name = 'Łukasz Warchał'
                        url = 'https://github.com/orzeh'
                    }
                }
            }
        }
    }
}

bintray {
    user = resolveProperty('BINTRAY_USER', 'bintrayUser')
    key = resolveProperty('BINTRAY_KEY', 'bintrayKey')
    publications = ['mavenJava']
    publish = true

    pkg {
        repo = 'gradle-plugins'
        name = 'com.bmuschko:gradle-docker-plugin'
        desc = 'Gradle plugin for managing Docker images and containers.'
        websiteUrl = "https://github.com/bmuschko/${project.name}"
        issueTrackerUrl = "https://github.com/bmuschko/${project.name}/issues"
        vcsUrl = "https://github.com/bmuschko/${project.name}.git"
        licenses = ['Apache-2.0']
        labels = ['gradle', 'docker', 'container', 'image', 'lightweight', 'vm', 'linux']
        publicDownloadNumbers = true
        githubRepo = "bmuschko/${project.name}"
        githubReleaseNotesFile = 'RELEASE_NOTES.md'

        version {
            released  = new Date()
            vcsTag = "v$project.version"
            attributes = ['gradle-plugin': ["com.bmuschko.docker-remote-api:${project.group}:${project.name}",
                                            "com.bmuschko.docker-java-application:${project.group}:${project.name}",
                                            "com.bmuschko.docker-spring-boot-application:${project.group}:${project.name}"]]

            gpg {
                sign = true
                passphrase = resolveProperty('GPG_PASSPHRASE', 'gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = resolveProperty('MAVEN_CENTRAL_USER_TOKEN', 'mavenCentralUserToken')
                password = resolveProperty('MAVEN_CENTRAL_PASSWORD', 'mavenCentralPassword')
                close = '1'
            }
        }
    }
}

String resolveProperty(String envVarKey, String projectPropKey) {
    String propValue = System.getenv()[envVarKey]

    if(propValue) {
        return propValue
    }

    hasProperty(projectPropKey) ? getProperty(projectPropKey) : null
}
