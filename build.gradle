buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.ajoberstar:gradle-git:1.7.1"
    }
}

group = 'com.bmuschko'

apply plugin: 'groovy'
apply plugin: 'org.ajoberstar.release-opinion'
apply from: "$rootDir/gradle/test-setup.gradle"
apply from: "$rootDir/gradle/integration-test.gradle"
apply from: "$rootDir/gradle/functional-test.gradle"
apply from: "$rootDir/gradle/additional-artifacts.gradle"
apply from: "$rootDir/gradle/release.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/documentation.gradle"
apply from: "$rootDir/gradle/codenarc.gradle"

repositories {
    jcenter()
}

dependencies {
    compile localGroovy(), gradleApi()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
}

ext.compatibilityVersion = '1.6'
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Docker plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
}
